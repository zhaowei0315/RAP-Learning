@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Travel projection view'
@Metadata.ignorePropagatedAnnotations: true
@Metadata.allowExtensions: true
define root view entity ZC_TRAVEL_APPROVER_FELIX_M
  provider contract transactional_query
  as projection on ZI_Travel_Felix_M
{
  key TravelId,
      @ObjectModel.text.element: [ 'AgencyName' ]
      AgencyId,
      _Agency.Name        as AgencyName,
      @ObjectModel.text: { element: [ 'CustomerName' ] }
      CustomerId,
      _Customer.FirstName as CustomerName,
      BeginDate,
      EndDate,
      @Semantics.amount.currencyCode: 'CurrencyCode'
      BookingFee,
      @Semantics.amount.currencyCode: 'CurrencyCode'
      TotalPrice,
      @ObjectModel.text: { element: [ 'CurrencyName' ] }
      CurrencyCode,
      _Currency._Text.CurrencyName             : localized,
      Description,
      @ObjectModel.text: { element: [ 'OverallStatusText' ] }
      OverallStatus,
      _Status._Text.Text  as OverallStatusText : localized,
      CreatedBy,
      CreatedAt,
      LastChangedBy,
      LastChangedAt,
      /* Associations */
      _Agency,
      _Booking : redirected to composition child ZC_BOOKING_APPROVER_FELIX_M,
      _Currency,
      _Customer,
      _Status
}
