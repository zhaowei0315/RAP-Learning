managed;// implementation in class zbp_r_travel_felix_d unique;
strict ( 2 );
with draft;

define behavior for ZR_TRAVEL_FELIX_D //alias <alias_name>
implementation in class zbp_travel_felix_d unique
persistent table /dmo/a_travel_d
draft table zwd_a_travel_d
lock master
total etag LastChangedAt//total etag field have to be different from etag master field
authorization master ( instance, global )
etag master LocalLastChangedAt
{
  create ( precheck );
  update ( precheck );
  delete;
  field ( numbering : managed, readonly ) TravelUUID;
  field ( readonly ) TravelID, OverallStatus, TotalPrice;
  field ( mandatory ) AgencyID, CustomerID, BookingFee;

  association _Booking { create ( authorization : update ); with draft; }

  action ( authorization : update ) acceptTravel result [1] $self;
  action ( authorization : update ) rejectTravel result [1] $self;
  action ( authorization : update ) deductDiscount parameter ZA_DEDUCT_DISCOUNT result [1] $self;
  internal action reCalcTotalPrice;

  determination setTravelID on save { create; }
  determination setOverallStatus on modify { create; }
  determination calcTotalPrice on modify { create; field BookingFee, CurrencyCode; }

  validation validateAgency on save { field AgencyID; create; }
  validation validateCustomer on save { field CustomerID; create; }
  validation validateDates on save { field BeginDate, EndDate; create; }

  //需要在conjunction layer头部添加 use side effects;
  side effects { field BookingFee affects field TotalPrice; }//to update the UI element Total Price when Booking Fee has changed

  //启用draft所需要定义的action
  draft action Resume with additional implementation;
  draft action Edit with additional implementation;
  draft action Activate optimized with additional implementation;
  draft action Discard with additional implementation;

  draft determine action Prepare
  {
    validation validateCustomer;
    validation validateAgency;
    validation validateDates;
    validation ZR_BOOKING_FELIX_D~validateCustomer;
    validation ZR_BookingSupplement_FELIX_D~validateSupplement;
  }

  mapping for /dmo/a_travel_d
    {
      AgencyID           = agency_id;
      BeginDate          = begin_date;
      BookingFee         = booking_fee;
      CurrencyCode       = currency_code;
      CustomerID         = customer_id;
      Description        = description;
      EndDate            = end_date;
      LastChangedAt      = last_changed_at;
      LocalCreatedAt     = local_created_at;
      LocalCreatedBy     = local_created_by;
      LocalLastChangedAt = local_last_changed_at;
      LocalLastChangedBy = local_last_changed_by;
      OverallStatus      = overall_status;
      TotalPrice         = total_price;
      TravelID           = travel_id;
      TravelUUID         = travel_uuid;
    }
}

define behavior for ZR_BOOKING_FELIX_D //alias <alias_name>
implementation in class zbp_booking_felix_d unique
persistent table /dmo/a_booking_d
draft table zwd_a_booking_d
lock dependent by _Travel
authorization dependent by _Travel
etag master LocalLastChangedAt
{
  update;
  delete;
  field ( readonly ) TravelUUID;
  field ( numbering : managed, readonly ) BookingUUID;
  field ( readonly ) BookingID;
  field ( readonly ) BookingDate;
  association _Travel { with draft; }
  association _BookingSupplement { create ( authorization : update ); with draft; }

  determination setBookingNumber on save { create; }
  determination setBookingDate on save { create; }
  determination calcTotalPrice on modify { create; field FlightPrice, CurrencyCode; }

  side effects { field FlightPrice affects field _Travel.TotalPrice; }//to update the UI element Total Price when FlightPrice has changed

  validation validateCustomer on save { field CustomerID; create; }

  mapping for /dmo/a_booking_d
    {
      AirlineID          = carrier_id;
      BookingDate        = booking_date;
      BookingID          = booking_id;
      BookingStatus      = booking_status;
      BookingUUID        = booking_uuid;
      ConnectionID       = connection_id;
      CurrencyCode       = currency_code;
      CustomerID         = customer_id;
      FlightDate         = flight_date;
      FlightPrice        = flight_price;
      LocalLastChangedAt = local_last_changed_at;
      TravelUUID         = parent_uuid;
    }
}

define behavior for ZR_BOOKINGSUPPLEMENT_FELIX_D //alias <alias_name>
implementation in class zbp_booksuppl_felix_d unique
persistent table /dmo/a_bksuppl_d
draft table zwd_a_bksuppl_d
lock dependent by _Travel
authorization dependent by _Travel
etag master LocalLastChangedAt
{
  update;
  delete;
  field ( readonly ) TravelUUID;
  field ( readonly ) BookingUUID;
  field ( readonly ) BookingSupplementID;
  field ( numbering : managed, readonly ) BookSupplUUID;
  field ( mandatory ) SupplementID;

  determination setBookSupplNumber on save { create; }
  determination calcTotalPrice on modify { create; field BookSupplPrice, CurrencyCode; }

  side effects { field BookSupplPrice affects field _Travel.TotalPrice; }//to update the UI element Total Price when BookSupplPrice has changed

  validation validateSupplement on save { field SupplementID; create; }

  association _Travel { with draft; }
  association _Booking { with draft; }

  mapping for /dmo/a_bksuppl_d
    {
      BookSupplPrice      = price;
      BookSupplUUID       = booksuppl_uuid;
      BookingSupplementID = booking_supplement_id;
      BookingUUID         = parent_uuid;
      CurrencyCode        = currency_code;
      LocalLastChangedAt  = local_last_changed_at;
      SupplementID        = supplement_id;
      TravelUUID          = root_uuid;
    }
}