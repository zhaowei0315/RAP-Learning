@Metadata.layer: #CORE
//https://developers.sap.com/group.fiori-tools-odata-v4-travel.html
//https://developers.sap.com/tutorials/fiori-tools-rap-modify-object-page..html
@UI.headerInfo: { typeName: 'Travel',
                  typeNamePlural: 'Travels',
                  title: { type: #STANDARD, value: 'TravelID' },
                  description: { value: 'Description' }
                }

@UI.presentationVariant: [ { sortOrder: [ { by: 'TravelID', direction: #DESC } ] } ]

annotate entity ZC_TRAVEL_FELIX_A_D with

{
  @UI.facet: [ { id: 'Travel',
                 purpose: #STANDARD,
                 type: #IDENTIFICATION_REFERENCE,
                 label: 'Travel Details',
                 position: 10 },
               { id: 'Booking',
                 purpose: #STANDARD,
                 type: #LINEITEM_REFERENCE,
                 label: 'Booking List',
                 position: 20,
                 targetElement: '_Booking' } ]
  @UI.hidden: true
  TravelUUID;

  @UI.identification: [ { position: 10, label: 'Travel ID' },
                        { type: #FOR_ACTION, dataAction: 'acceptTravel', label: 'Accept Travel' },
                        { type: #FOR_ACTION, dataAction: 'rejectTravel', label: 'Reject Travel' },
                        { type: #FOR_ACTION, dataAction: 'deductDiscount', label: 'Deduct Discount' } ]
  @UI.lineItem: [ { position: 10, importance: #HIGH },
                  { type: #FOR_ACTION, dataAction: 'acceptTravel', label: 'Accept Travel' },
                  { type: #FOR_ACTION, dataAction: 'rejectTravel', label: 'Reject Travel' },
                  { type: #FOR_ACTION, dataAction: 'deductDiscount', label: 'Deduct Discount', invocationGrouping: #CHANGE_SET } ]
  @UI.selectionField: [ { position: 10 } ]
  TravelID;

  @UI.identification: [ { position: 20 } ]
  @UI.lineItem: [ { position: 20 } ]
  @UI.selectionField: [ { position: 20 } ]
  AgencyID;

  @UI.identification: [ { position: 30 } ]
  @UI.lineItem: [ { position: 30 } ]
  @UI.selectionField: [ { position: 30 } ]
  CustomerID;

  @UI.identification: [ { position: 40 } ]
  @UI.lineItem: [ { position: 40 } ]
  BeginDate;

  @UI.identification: [ { position: 50 } ]
  @UI.lineItem: [ { position: 50 } ]
  EndDate;

  @UI.identification: [ { position: 60 } ]
  @UI.lineItem: [ { position: 60 } ]
  BookingFee;

  @UI.identification: [ { position: 70 } ]
  @UI.lineItem: [ { position: 70 } ]
  TotalPrice;

  //  CurrencyCode;

  @UI.identification: [ { position: 90 } ]
  @UI.lineItem: [ { position: 90 } ]
  Description;

  @UI.identification: [ { position: 99, label: 'Status' } ]
  @UI.lineItem: [ { position: 99 } ]
  @UI.selectionField: [ { position: 40 } ]
  @UI.textArrangement: #TEXT_ONLY
  OverallStatus;

  @UI.hidden: true
  OverallStatusText;

  @UI.hidden: true
  LocalLastChangedAt;
}