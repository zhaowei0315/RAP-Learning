@AbapCatalog.viewEnhancementCategory: [#NONE]
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'reference to /DMO/CONNECTION'
@Metadata.ignorePropagatedAnnotations: true
@ObjectModel.usageType:{
    serviceQuality: #X,
    sizeCategory: #S,
    dataClass: #MIXED
}
@UI.headerInfo: {
    typeName: 'Connection',
    typeNamePlural: 'Connections'
}
@Search.searchable: true
define view entity ZI_CONNECTION_FELIX
  as select from /dmo/connection as Connection
  association [1..*] to ZI_Flight_FELIX_R  as _Flight  on  $projection.CarrierId    = _Flight.CarrierId
                                                       and $projection.ConnectionId = _Flight.ConnectionId
  association [1]    to ZI_Carrier_Felix_R as _Airline on  $projection.CarrierId = _Airline.CarrierId

{
      @UI.facet: [{
          id: 'Connection',
          purpose: #STANDARD,
          position: 10,
          label: 'Connection Details',
          type: #IDENTIFICATION_REFERENCE
      },
      {
          id: 'Flight',
          purpose: #STANDARD,
          position: 20,
          label: 'Flight List',
          type: #LINEITEM_REFERENCE,
          targetElement: '_Flight'
      }]

      @UI.identification: [{ position: 10, label: 'Airline' }]
      @UI.lineItem: [{ position: 10 }]
      //@UI.selectionField: [{ position: 10 }]
      @ObjectModel.text.association: '_Airline'
      @Search.defaultSearchElement: true
  key carrier_id      as CarrierId,

      @UI.identification: [{ position: 20 }]
      //@UI.selectionField: [{ position: 20 }]
      @UI.lineItem: [{ position: 20 }]
  key connection_id   as ConnectionId,

      @UI.identification: [{ position: 30, label: 'Departure Airport ID' }]
      @UI.lineItem: [{ position: 30, label: 'Departure Airport ID' }]
      @UI.selectionField: [{ position: 30 }]
      @Search.defaultSearchElement: true
      @Consumption.valueHelpDefinition: [{ entity: {
          name: 'ZI_Airport_Felix_VH',
          element: 'AirportId'
      } }]
      airport_from_id as AirportFromId,

      @UI.identification: [{ position: 40, label: 'Arriving Airport ID' }]
      @UI.lineItem: [{ position: 40, label: 'Arriving Airport ID'}]
      @UI.selectionField: [{ position: 40 }]
      @Search.defaultSearchElement: true
      @Consumption.valueHelpDefinition: [{ entity: {
          name: 'ZI_Airport_Felix_VH',
          element: 'AirportId'
      } }]
      @EndUserText.label: 'Destination Airport ID'//effect everywhere text
      airport_to_id   as AirportToId,

      @UI.identification: [{ position: 50 }]
      @UI.lineItem: [{ position: 50, label:'Departure Time' }]
      departure_time  as DepartureTime,

      @UI.identification: [{ position: 60 }]
      @UI.lineItem: [{ position: 60, label:'Arrival Time' }]
      arrival_time    as ArrivalTime,

      @UI.identification: [{ position: 70 }]
      @UI.lineItem: [{ position: 70 }]
      @Semantics.quantity.unitOfMeasure: 'DistanceUnit'
      distance        as Distance,

      distance_unit   as DistanceUnit,

      //Association
      @Search.defaultSearchElement: true
      _Flight,

      @Search.defaultSearchElement: true
      _Airline
}
